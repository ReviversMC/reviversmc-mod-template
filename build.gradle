plugins {
	id 'fabric-loom'
	id 'io.github.juuxel.loom-quiltflower'
	id 'checkstyle'
}


allprojects {
	apply plugin: 'fabric-loom'
	apply plugin: 'io.github.juuxel.loom-quiltflower'
	apply plugin: 'checkstyle'

	checkstyle {
		configFile = rootProject.file('checkstyle.xml')
		toolVersion = rootProject.checkstyle_version
	}


	version = rootProject.mod_version
	group = rootProject.maven_group

	base {
		archivesName = rootProject.archives_base_name
	}


	configurations {
		modRuntimeOnly {
			transitive = true
			exclude module: 'fabric-loader'
		}
	}

	repositories {
		if (project.use_third_party_mods == 'true') {
			maven {
				name = 'Modrinth'
				url = 'https://api.modrinth.com/maven'
				content {
					includeGroup 'maven.modrinth'
				}
			}
			maven {
				url = 'https://cursemaven.com'
				content {
					includeGroup 'curse.maven'
				}
			}
		}
		maven {
			name ='Shedaniel'
			url = 'https://maven.shedaniel.me/'
		}
		maven {
			name = 'TerraformersMC'
			url = 'https://maven.terraformersmc.com/releases'
		}
		mavenCentral()
		mavenLocal()
	}

	dependencies {
		modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
	}


	processResources {
		inputs.property 'version', project.version

		filesMatching('fabric.mod.json') {
			expand 'version': project.version
		}
	}

	tasks.withType(JavaCompile).configureEach {
		// Ensure that the encoding is set to UTF-8, no matter what the system default is.
		// This fixes some edge cases with special characters not displaying correctly,
		// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html.
		// If Javadoc is generated, this must be specified in that task too.
		it.options.encoding = 'UTF-8'
	}

	java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
		// if it is present. If you remove this line, sources will not be generated.
		withSourcesJar()

		// If this mod is going to be a library, then it should also generate Javadocs in order to aid
		// with development. Uncomment this line to generate them.
		// withJavadocJar()
	}
}


dependencies {
	include implementation(project(path: ':modtemplate-core', configuration: 'namedElements'))
	include implementation(project(path: ':modtemplate-1.16-compat', configuration: 'namedElements'))
	include implementation(project(path: ':modtemplate-1.17-compat', configuration: 'namedElements'))
	include implementation(project(path: ':modtemplate-1.18.2-compat', configuration: 'namedElements'))
	include implementation(project(path: ':modtemplate-1.19-compat', configuration: 'namedElements'))
	include implementation(project(path: ':modtemplate-1.19.3-compat', configuration: 'namedElements'))
	include implementation(project(path: ':modtemplate-1.19.4-compat', configuration: 'namedElements'))
	include implementation(project(path: ':modtemplate-1.20-compat', configuration: 'namedElements'))

	minecraft "com.mojang:minecraft:${minecraft_version_1_20}"
	mappings "net.fabricmc:yarn:${yarn_mappings_1_20}:v2"

	modRuntimeOnly fabricApi.module('fabric-api-base', project.fabric_api_version_1_20)
	modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version_1_20}"
	modRuntimeOnly "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version_1_20}"
}


processResources {
	from("${rootDir}/assets/icon.png") {
		into "assets/modtemplate/"
	}
}
